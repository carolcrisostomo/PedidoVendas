unit uPedidoItemDAO;

interface

uses
  FireDAC.Comp.Client, System.Generics.Collections, uPedidoItem;

type
  TPedidoItemDAO = class
  public
    class procedure Inserir(Conn: TFDConnection; Item: TPedidoItem);
    class procedure Atualizar(Conn: TFDConnection; Item: TPedidoItem);
    class procedure Excluir(Conn: TFDConnection; AutoInc: Integer);
    class function CarregarItens(Conn: TFDConnection; NumeroPedido: Integer): TObjectList<TPedidoItem>;
  end;

implementation

class procedure TPedidoItemDAO.Inserir(Conn: TFDConnection; Item: TPedidoItem);
var
  Q: TFDQuery;
begin
  Q := TFDQuery.Create(nil);
  try
    Q.Connection := Conn;
    Q.SQL.Text :=
      'INSERT INTO PEDIDOS_PRODUTOS '+
      '(NUMERO_PEDIDO, CODIGO_PRODUTO, QUANTIDADE, VLR_UNITARIO, VLR_TOTAL) '+
      'VALUES (:NUM, :PROD, :QTD, :UNIT, :TOTAL)';

    Q.ParamByName('NUM').AsInteger := Item.NumeroPedido;
    Q.ParamByName('PROD').AsInteger := Item.CodigoProduto;
    Q.ParamByName('QTD').AsFloat := Item.Quantidade;
    Q.ParamByName('UNIT').AsCurrency := Item.ValorUnitario;
    Q.ParamByName('TOTAL').AsCurrency := Item.ValorTotal;
    Q.ExecSQL;
  finally
    Q.Free;
  end;
end;

class procedure TPedidoItemDAO.Atualizar(Conn: TFDConnection; Item: TPedidoItem);
var
  Q: TFDQuery;
begin
  Q := TFDQuery.Create(nil);
  try
    Q.Connection := Conn;
    Q.SQL.Text :=
      'UPDATE PEDIDOS_PRODUTOS SET '+
      'QUANTIDADE = :QTD, '+
      'VLR_UNITUARIO = :UNIT, '+
      'VLR_TOTAL = :TOTAL '+
      'WHERE AUTOINCREM = :ID';

    Q.ParamByName('ID').AsInteger := Item.AutoInc;
    Q.ParamByName('QTD').AsFloat := Item.Quantidade;
    Q.ParamByName('UNIT').AsCurrency := Item.ValorUnitario;
    Q.ParamByName('TOTAL').AsCurrency := Item.ValorTotal;
    Q.ExecSQL;
  finally
    Q.Free;
  end;
end;

class procedure TPedidoItemDAO.Excluir(Conn: TFDConnection; AutoInc: Integer);
var
  Q: TFDQuery;
begin
  Q := TFDQuery.Create(nil);
  try
    Q.Connection := Conn;
    Q.SQL.Text := 'DELETE FROM PEDIDOS_PRODUTOS WHERE AUTOINCREM = :ID';
    Q.ParamByName('ID').AsInteger := AutoInc;
    Q.ExecSQL;
  finally
    Q.Free;
  end;
end;

class function TPedidoItemDAO.CarregarItens(Conn: TFDConnection; NumeroPedido: Integer): TObjectList<TPedidoItem>;
var
  Q: TFDQuery;
  Item: TPedidoItem;
begin
  Result := TObjectList<TPedidoItem>.Create(True);

  Q := TFDQuery.Create(nil);
  try
    Q.Connection := Conn;
    Q.SQL.Text :=
      'SELECT AUTOINCREM, CODIGO_PRODUTO, QUANTIDADE, VLR_UNITARIO, VLR_TOTAL '+
      'FROM PEDIDOS_PRODUTOS WHERE NUMERO_PEDIDO = :NUM';

    Q.ParamByName('NUM').AsInteger := NumeroPedido;
    Q.Open;

    while not Q.Eof do
    begin
      Item := TPedidoItem.Create;
      Item.AutoInc := Q.FieldByName('AUTOINCREM').AsInteger;
      Item.NumeroPedido := NumeroPedido;
      Item.CodigoProduto := Q.FieldByName('CODIGO_PRODUTO').AsInteger;
      Item.Quantidade := Q.FieldByName('QUANTIDADE').AsFloat;
      Item.ValorUnitario := Q.FieldByName('VLR_UNITARIO').AsCurrency;
      Item.ValorTotal := Q.FieldByName('VLR_TOTAL').AsCurrency;

      Result.Add(Item);
      Q.Next;
    end;
  finally
    Q.Free;
  end;
end;

end.
